apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://dl.dropboxusercontent.com/u/3039847/maven-repository'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.tughi.memoria"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 10101
        versionName "1.1.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.tughi.android:sqlite-open-helper:0.5'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.3'
}

task generateDrawables << {
    if ("which inkscape".execute().waitFor() != 0) {
        println("Inkscape could not be found in the PATH.")
        return
    }

    def svgDir = new File(projectDir, ['src', 'main', 'svg'].join(File.separator))
    def resDir = new File(projectDir, ['src', 'main', 'res'].join(File.separator))

    def DENSITIES = [
            'mdpi'   : 90,
            'hdpi'   : 135,
            'xhdpi'  : 180,
            'xxhdpi' : 270,
            'xxxhdpi': 360
    ]

    def generate = { String drawable ->
        new File(svgDir, drawable.replace('-*', '')).eachFileMatch(~/.*\.svg/) { svg ->
            DENSITIES.each { type, dpi ->
                def png = new File(new File(resDir, drawable.replace('*', type)), svg.name.replace('.svg', '.png'))
                if (!png.exists() || png.lastModified() < svg.lastModified()) {
                    png.parentFile.mkdirs()
                    "inkscape -e $png -y 0 -d$dpi $svg"
                            .execute()
                            .waitFor()
                }
            }
        }
    }

    generate('drawable-*')
//    generate('drawable-*-v11')
//    generate('mipmap-*')
}

preBuild.dependsOn generateDrawables
