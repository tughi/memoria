apply plugin: 'com.android.application'

repositories {
    mavenCentral()
    maven {
        url 'https://dl.dropboxusercontent.com/u/3039847/maven-repository'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.tughi.memoria"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile 'com.tughi.android:sqlite-open-helper:0.5'
}

task generateDrawables << {
    if ("which inkscape".execute().waitFor() != 0) {
        println("Inkscape could not be found in the PATH.")
        return
    }

    def svgDir = new File(projectDir, ['src', 'main', 'svg'].join(File.separator))
    def resDir = new File(projectDir, ['src', 'main', 'res'].join(File.separator))

    def DENSITIES = [
            'mdpi'   : 90,
            'hdpi'   : 135,
            'xhdpi'  : 180,
            'xxhdpi' : 270,
            'xxxhdpi': 360
    ]

    def generate = { String drawable ->
        new File(svgDir, drawable.replace('-*', '')).eachFileMatch(~/.*\.svg/) { svg ->
            DENSITIES.each { type, dpi ->
                def png = new File(new File(resDir, drawable.replace('*', type)), svg.name.replace('.svg', '.png'))
                if (!png.exists() || png.lastModified() < svg.lastModified()) {
                    png.parentFile.mkdirs()
                    "inkscape -e $png -y 0 -d$dpi $svg"
                            .execute()
                            .waitFor()
                }
            }
        }
    }

    generate('drawable-*')
//    generate('drawable-*-v11')
//    generate('mipmap-*')
}

preBuild.dependsOn generateDrawables
